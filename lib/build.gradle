plugins {
    id 'java-library'
    id 'checkstyle'
    id 'org.ec4j.editorconfig' version '0.0.3'
    id "com.github.spotbugs" version "5.0.13"
}

sourceCompatibility = '17'
compileJava.options.encoding = 'UTF-8'
group = 'io.github.sang-hyeon'
version = artifactVersion

repositories {
    mavenCentral()
}

dependencies {
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    implementation 'com.google.guava:guava:31.1-jre'

    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

editorconfig {
    excludes = ["build"]
}
check.dependsOn editorconfigCheck

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    configProperties = ["suppressionFile": "${project.rootDir}/config/checkstyle/checkstyle-suppressions.xml"]
    toolVersion = "10.3"
    ignoreFailures = true
    maxErrors = 0
    maxWarnings = 0
}

spotbugs {
    toolVersion = '4.7.2'
    showStackTraces = true
    ignoreFailures = true
    reportLevel = 'high'
}

jar {
    manifest {
        attributes(
            'Specification-Title': artifactName,
            'Specification-Version': artifactVersion,
            'Specification-Vendor': 'io.github.readonlycollections',
            'Implementation-Title': artifactName,
            'Implementation-Version': artifactVersion,
            'Implementation-Vendor': 'io.github.readonlycollections'
        )
    }
}
